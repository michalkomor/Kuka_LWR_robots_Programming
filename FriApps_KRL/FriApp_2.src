&ACCESS RVP
&REL 73
&PARAM TEMPLATE = C:\KRC\Roboter\Template\vorgabe
&PARAM EDITMASK = *
DEF FriApp_2( )

;fold stiffness
DECL STIFFNESS MYSTIFF
MYSTIFF=$STIFFNESS

;FOLD ; controller type: (10, 20, 30, 101);%{PE}%R 5.6.11,%MKUKATPBASIS,%CCOMMENT,%VNORMAL,%P 2:controller type: (10, 20, 30, 101)
;ENDFOLD
MYSTIFF.STRATEGY=20
;FOLD ; reference coordinate system: (#tool or #base);%{PE}%R 5.6.11,%MKUKATPBASIS,%CCOMMENT,%VNORMAL,%P 2:reference coordinate system: (#tool or #base)
;ENDFOLD
MYSTIFF.FRAMETYPE=#TOOL
;FOLD ; tool:;%{PE}%R 5.6.11,%MKUKATPBASIS,%CCOMMENT,%VNORMAL,%P 2:tool:
;ENDFOLD
MYSTIFF.TOOL=TOOL_DATA[3]
;FOLD ; Cartesian spring stiffness:;%{PE}%R 5.6.11,%MKUKATPBASIS,%CCOMMENT,%VNORMAL,%P 2:Cartesian spring stiffness:
;ENDFOLD

;MYSTIFF.CPSTIFFNESS    = { X 0.0,Y 0.0,Z 0.0, A 0.0, B 0.0,C 0.0}  
MYSTIFF.CPSTIFFNESS    = { X 100.0,Y 1000.0,Z 3000.0, A 100.0, B 100.0,C 100.0}  
;FOLD ; Cartesian spring damping:;%{PE}%R 5.6.11,%MKUKATPBASIS,%CCOMMENT,%VNORMAL,%P 2:Cartesian spring damping:
;ENDFOLD
MYSTIFF.CPDAMPING      = { X 0.7,Y 0.7,Z 0.7,A 0.7,B 0.7,C 0.7}
;FOLD ; Axis-specific spring stiffness:;%{PE}%R 5.6.11,%MKUKATPBASIS,%CCOMMENT,%VNORMAL,%P 2:Axis-specific spring stiffness:
;ENDFOLD
;MYSTIFF.AXISSTIFFNESS  = {A1 0.0,A2 0.0,A3 0.0,A4 0.0,A5 0.0,A6 0.0,E1 0.0}
MYSTIFF.AXISSTIFFNESS  = {A1 200.0,A2 500.0,A3 300.0,A4 1000.0,A5 2000.0,A6 2000.0,E1 2000.0}
;FOLD ; Axis-specific spring damping:;%{PE}%R 5.6.11,%MKUKATPBASIS,%CCOMMENT,%VNORMAL,%P 2:Axis-specific spring damping:
;ENDFOLD
 MYSTIFF.AXISDAMPING   = {A1 0.7,A2 0.7,A3 0.7, A4 0.7, A5 0.7, A6 0.7, E1 0.7} 
;FOLD ; Limit. of the Cartesian deviation from the path:;%{PE}%R 5.6.11,%MKUKATPBASIS,%CCOMMENT,%VNORMAL,%P 2:Limit. of the Cartesian deviation from the path:
;ENDFOLD
MYSTIFF.CPMAXDELTA     = {X 1000.0,Y 1000.0,Z 1000.0,A 1000.0,B 1000.0,C 1000.0} 
;FOLD ; Limit. of the Cartesian force at the TCP:;%{PE}%R 5.6.11,%MKUKATPBASIS,%CCOMMENT,%VNORMAL,%P 2:Limit. of the Cartesian force at the TCP:
;ENDFOLD
MYSTIFF.MAXFORCE       = {X 150.0,Y 150.0,Z 150.0,A 150.0,B 150.0,C 150.0} 
;FOLD ; Limit. of the axis-specific deviation:;%{PE}%R 5.6.11,%MKUKATPBASIS,%CCOMMENT,%VNORMAL,%P 2:Limit. of the axis-specific deviation:
;ENDFOLD
MYSTIFF.AXISMAXDELTA   = {A1 0.0,A2 0.0,A3 0.0,A4 0.0,A5 0.0, A6 0.0, E1 0.0}
;FOLD ; Limit. of the axis-specific torque:;%{PE}%R 5.6.11,%MKUKATPBASIS,%CCOMMENT,%VNORMAL,%P 2:Limit. of the axis-specific torque:
;ENDFOLD
MYSTIFF.AXISMAXDELTATRQ= {A1 100.0,A2 100.0,A3 100.0,A4 100.0,A5 100.0, A6 100.0, E1 100.0}
;endfold stiffness

;FOLD INI
  ;FOLD BASISTECH INI

    GLOBAL INTERRUPT DECL 3 WHEN $STOPMESS==TRUE DO IR_STOPM ( )
    INTERRUPT ON 3 
    BAS (#INITMOV,0 )
  ;ENDFOLD (BASISTECH INI)
  ;FOLD USER INI
    ;Make your modifications here
    

  ;ENDFOLD (USER INI)
;ENDFOLD (INI)
for i=1 to 16
$fri_to_int[i]=0
$fri_to_rea[i]=0.0
endfor


$stiffness.strategy=10
$stiffness.commit=true
; set the tool
bas(#tool,3) ;3 - empty flange


;move to Home-Position
;ptp { a1 0,a2 90,a3 0,a4 0,a5 0,a6 0,e1 0}

; Move to Start Position
ptp {a1 0,a2 87,a3 87,a4 0,a5 -55,a6 0,e1 0}
HALT


; Test open FRI with datarate 5 msec
retVal=friopen(5)
wait for ($FriState==#MON)
$fri_to_rea[1]=0.123
$fri_to_int[1]=10
wait sec 0.2

for i=1 to 3
ptp {a1 0,a2 87,a3 87,a4 0,a5 -55,a6 0,e1 0}
$STIFFNESS=MYSTIFF
wait for ($FriQuality==#PERFECT)
retVal=FRISTART(1.0)
wait for ($FriState==#CMD)

$FRI_TO_INT[2]=i
wait sec 10


retVal=FRISTOP()

$stiffness.strategy=10
$stiffness.commit=true
endfor


$stiffness.strategy=10
$stiffness.commit=true
ptp {a1 0,a2 87,a3 87,a4 0,a5 -55,a6 0,e1 0}
$stiffness.strategy=30
$stiffness.commit=true

wait for ($FriQuality==#PERFECT)
retVal=FRISTART(1.0)
wait for ($FriState==#CMD)

wait sec 15


retVal=FRISTOP()

$fri_to_int[1]=-1
retVal=FRICLOSE()

$stiffness.strategy=10
$stiffness.commit=true

;move to Home-Position
ptp { a1 0,a2 90,a3 0,a4 0,a5 0,a6 0,e1 0}
END